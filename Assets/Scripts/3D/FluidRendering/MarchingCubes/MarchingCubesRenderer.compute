#pragma kernel ResetVerticesCounter
#pragma kernel MarchCubes
#pragma kernel PrepareDrawIndirectArgs

// Helpers
#include "MarchingTables.hlsl"
#include "MarchingCubesShared.hlsl"


// Structs

struct CubeSample {
    Sample samples[8];
};

struct IndirectDrawArgs {
    uint vertexCountPerInstance;
    uint instanceCount;
    uint startVertex;
    uint startInstance;
};


// Shader properties (Common data)
int _maxNumVertices;


// Structured buffers (Per instance data)
RWStructuredBuffer<float3> _Vertices;
RWStructuredBuffer<float3> _Normals;
RWStructuredBuffer<uint> _VerticesCounter;
RWStructuredBuffer<IndirectDrawArgs> _IndirectDrawArgs;


// Helper functions

int ComputeCubeIndex(CubeSample cubeSample) {
    int cubeIndex = 0;

    if (cubeSample.samples[0].value >= _isoLevel) { cubeIndex |= 1; }
    if (cubeSample.samples[1].value >= _isoLevel) { cubeIndex |= 2; }
    if (cubeSample.samples[2].value >= _isoLevel) { cubeIndex |= 4; }
    if (cubeSample.samples[3].value >= _isoLevel) { cubeIndex |= 8; }
    if (cubeSample.samples[4].value >= _isoLevel) { cubeIndex |= 16; }
    if (cubeSample.samples[5].value >= _isoLevel) { cubeIndex |= 32; }
    if (cubeSample.samples[6].value >= _isoLevel) { cubeIndex |= 64; }
    if (cubeSample.samples[7].value >= _isoLevel) { cubeIndex |= 128; }

    return cubeIndex;
}

CubeSample LoadSamples(int3 pos) {
    CubeSample cubeSample = {{ 
        _Samples[IndexFromCoords(pos.x    , pos.y    , pos.z    )],
        _Samples[IndexFromCoords(pos.x + 1, pos.y    , pos.z    )],
        _Samples[IndexFromCoords(pos.x + 1, pos.y    , pos.z + 1)],
        _Samples[IndexFromCoords(pos.x    , pos.y    , pos.z + 1)],
        _Samples[IndexFromCoords(pos.x    , pos.y + 1, pos.z    )],
        _Samples[IndexFromCoords(pos.x + 1, pos.y + 1, pos.z    )],
        _Samples[IndexFromCoords(pos.x + 1, pos.y + 1, pos.z + 1)],
        _Samples[IndexFromCoords(pos.x    , pos.y + 1, pos.z + 1)]
    }};

    return cubeSample;
}

float3 InterpolateSamples(Sample a, Sample b) {
    float t = (_isoLevel - a.value) / (b.value - a.value);
    return a.position + t * (b.position - a.position);
}


// Kernels

[numthreads(1, 1, 1)]
void ResetVerticesCounter (uint3 id : SV_DispatchThreadID) {
    _VerticesCounter[0] = 0;
}

[numthreads(BLOCK_SIZE_AXIS, BLOCK_SIZE_AXIS, BLOCK_SIZE_AXIS)]
void MarchCubes (uint3 id : SV_DispatchThreadID) {
    if (IsVectorOutOfRange(id, _samplesPerAxis - 1)) { return; }

    // Compute the configuration
    CubeSample cubeSample = LoadSamples(id);
    int cubeIndex = ComputeCubeIndex(cubeSample);
    if (cubeIndex == 0 || cubeIndex == 255) { return; } // No triangles cases

    // Count the number of triangles, reserve spot for the vertices
    int numTriangles = GetNumberOfTriangles(cubeIndex);
    int numVertices = numTriangles * 3;
    int vertexBaseIdx;
    InterlockedAdd(_VerticesCounter[0], numTriangles * 3, vertexBaseIdx);
    if (vertexBaseIdx + numVertices > _maxNumVertices) { return; } // If there is not enough space in the buffers

    // Build Vertices and Triangles
    for (int i = 0; i < numVertices; i += 3) {
        int triangleStartIndex  = vertexBaseIdx + i;

        // Get edge vertices
        int2 edge0Verts = edgeConnection[triangleConnections[cubeIndex][i]];
        int2 edge1Verts = edgeConnection[triangleConnections[cubeIndex][i + 1]];
        int2 edge2Verts = edgeConnection[triangleConnections[cubeIndex][i + 2]];

        // Interpolate the edge vertices
        float3 vA = InterpolateSamples(cubeSample.samples[edge0Verts.x], cubeSample.samples[edge0Verts.y]);
        float3 vB = InterpolateSamples(cubeSample.samples[edge1Verts.x], cubeSample.samples[edge1Verts.y]);
        float3 vC = InterpolateSamples(cubeSample.samples[edge2Verts.x], cubeSample.samples[edge2Verts.y]);

        // TODO: Compute normals
        float3 u = vB - vA;
        float3 v = vC - vA;
        float3 n = normalize(cross(u, v));

        // Save the vertices
        _Vertices[triangleStartIndex] = vA;
        _Vertices[triangleStartIndex + 1] = vB;
        _Vertices[triangleStartIndex + 2] = vC;

        // Save the normals
        _Normals[triangleStartIndex] = n;
        _Normals[triangleStartIndex + 1] = n;
        _Normals[triangleStartIndex + 2] = n;
    }
}

[numthreads(1, 1, 1)]
void PrepareDrawIndirectArgs (uint3 id : SV_DispatchThreadID) {
    _IndirectDrawArgs[0].instanceCount = 1;
    _IndirectDrawArgs[0].startInstance = 0;
    _IndirectDrawArgs[0].startVertex = 0;
    _IndirectDrawArgs[0].vertexCountPerInstance = _VerticesCounter[0];
}