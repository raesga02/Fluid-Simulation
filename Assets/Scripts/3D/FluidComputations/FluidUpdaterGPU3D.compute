// Kernels
#pragma kernel ApplyExternalForces
#pragma kernel ComputeSpatialHashes
#pragma kernel ResetHashLookup
#pragma kernel BitonicMergeStep
#pragma kernel BuildSpatialHashLookup
#pragma kernel CalculateDensities
#pragma kernel CalculatePressures
#pragma kernel ApplyPressureForce
#pragma kernel ApplyViscosityForce
#pragma kernel IntegratePosition
#pragma kernel HandleCollisions

#define BLOCK_SIZE 64

// Helpers
#include "FluidPhysicsHelper3D.hlsl"
#include "SpatialHashHelper3D.hlsl"
#include "../FluidColliders/FluidCollisionsHelper.hlsl"

// Structs

struct HashLookup {
    int size;
    int first;
};


// Shader properties (Common data)
float _deltaTime;
int _numParticles;
float3 _gravity;
float _particleMass;
float _particleRadius;
float _smoothingLength;
float _restDensity;
float _bulkModulus;
float _dynamicViscosity;

int _paddedNumParticles;
int _mergeSize;
int _compareDist;

int _numColliders;
float _collisionDamping;


// Structured buffers (Per instance data)
RWStructuredBuffer<float3> _Positions;
RWStructuredBuffer<float3> _PredictedPositions;
RWStructuredBuffer<float3> _Velocities;
RWStructuredBuffer<float> _Densities;
RWStructuredBuffer<float> _Pressures;
RWStructuredBuffer<int2> _SortedSpatialHashedIndices;
RWStructuredBuffer<HashLookup> _LookupHashIndices;


// Helper functions

bool IndexOutOfRange(int particleIndex, int max) {
    return particleIndex >= max;
}

int Compare(int i, int j) {
    return CompareKeyFromHash(_SortedSpatialHashedIndices[i][1], _SortedSpatialHashedIndices[j][1], _numParticles * 2);
}

void Exchange(int i, int j) {
    int2 tmp = _SortedSpatialHashedIndices[i];
    _SortedSpatialHashedIndices[i] = _SortedSpatialHashedIndices[j];
    _SortedSpatialHashedIndices[j] = tmp;
}


// Kernels

[numthreads(BLOCK_SIZE, 1, 1)]
void ApplyExternalForces(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }
    _Velocities[id.x] += _gravity * _deltaTime;
    _PredictedPositions[id.x] = _Positions[id.x] + _Velocities[id.x] * _deltaTime;
}

[numthreads(BLOCK_SIZE, 1, 1)]
void ComputeSpatialHashes(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }
    
    int3 cellPos = GetCellPos(_PredictedPositions[id.x], _smoothingLength);
    int hash = ComputeHash(cellPos);
    _SortedSpatialHashedIndices[id.x] = int2(id.x, hash);
}

[numthreads(BLOCK_SIZE, 1, 1)]
void ResetHashLookup(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles * 2)) { return; }
    _LookupHashIndices[id.x].size = 0;
    _LookupHashIndices[id.x].first = 0x7FFFFFFF;
}

[numthreads(BLOCK_SIZE, 1, 1)]
void BitonicMergeStep(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _paddedNumParticles)) { return; }
    int i = id.x;
    int j = i ^ _compareDist;

    if (j > i) {
        if (((i & _mergeSize) == 0) == (Compare(i, j) > 0)) {
            Exchange(i, j);
        }
    }
}

[numthreads(BLOCK_SIZE, 1, 1)]
void BuildSpatialHashLookup(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }

    int hash = _SortedSpatialHashedIndices[id.x][1];
    int key = GetKey(hash, _numParticles * 2);
    int candidateFirst = id.x;

    InterlockedAdd(_LookupHashIndices[key].size, 1);
    InterlockedMin(_LookupHashIndices[key].first, candidateFirst);
}

[numthreads(BLOCK_SIZE, 1, 1)]
void CalculateDensities(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }

    float3 srcPos = _PredictedPositions[id.x];
    int3 srcCellPos = GetCellPos(srcPos, _smoothingLength);
    
    float rawKernelAcc = 0.0;
    for (int i = 0; i < NEIGHBOURHOOD_SIZE; i++) {
        int3 nbrCellPos = srcCellPos + neighbourOffsets[i];
        int nbrHash = ComputeHash(nbrCellPos);
        int nbrKey = GetKey(nbrHash, _numParticles * 2);
        HashLookup lookupData = _LookupHashIndices[nbrKey];

        if (lookupData.size == 0) { continue; } // No particles with that key
        
        for (int j = lookupData.first; j < lookupData.first + lookupData.size; j++) {
            if (nbrHash != _SortedSpatialHashedIndices[j][1]) { continue; } // Avoiding key collisions
            
            int nbrIdx = _SortedSpatialHashedIndices[j][0];
            float3 r = _PredictedPositions[nbrIdx] - srcPos;
            float r2 = dot(r, r);

            if (r2 >= _smoothingLength * _smoothingLength) { continue; } // Discard far away particles

            float rL = sqrt(r2);
            rawKernelAcc += DensityKernel(rL, _smoothingLength);
        }
    }

    _Densities[id.x] = _particleMass * rawKernelAcc;
}

[numthreads(BLOCK_SIZE, 1, 1)]
void CalculatePressures(int3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }
    _Pressures[id.x] = LinearStateEquation(_Densities[id.x], _restDensity, _bulkModulus);
}

[numthreads(BLOCK_SIZE, 1, 1)]
void ApplyPressureForce(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }

    float3 srcPos = _PredictedPositions[id.x];
    float srcDensity = _Densities[id.x];
    float srcPressure = _Pressures[id.x];
    int3 srcCellPos = GetCellPos(srcPos, _smoothingLength);
    
    float3 pressureGradient = float3(0.0, 0.0, 0.0);
    for (int i = 0; i < NEIGHBOURHOOD_SIZE; i++) {
        int3 nbrCellPos = srcCellPos + neighbourOffsets[i];
        int nbrHash = ComputeHash(nbrCellPos);
        int nbrKey = GetKey(nbrHash, _numParticles * 2);
        HashLookup lookupData = _LookupHashIndices[nbrKey];

        if (lookupData.size == 0) { continue; } // No particles with that key
        
        for (int j = lookupData.first; j < lookupData.first + lookupData.size; j++) {
            if (nbrHash != _SortedSpatialHashedIndices[j][1]) { continue; } // Avoiding key collisions

            int nbrIdx = _SortedSpatialHashedIndices[j][0];
            float3 r = _PredictedPositions[nbrIdx] - srcPos;
            float r2 = dot(r, r);
            
            if (r2 >= _smoothingLength * _smoothingLength) { continue; } // Discard far away particles
            
            float nbrDensity = _Densities[nbrIdx];
            float nbrPressure = _Pressures[nbrIdx];
            float sharedPressureFactor = (srcPressure + nbrPressure) / (nbrDensity * 2.0);
            pressureGradient += sharedPressureFactor * PressureKernel(r, _smoothingLength);
        }
    }

    pressureGradient = _particleMass * pressureGradient;
    float3 pressureForce = pressureGradient / srcDensity;
    _Velocities[id.x] += pressureForce * _deltaTime;
}

[numthreads(BLOCK_SIZE, 1, 1)]
void ApplyViscosityForce(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }

    float3 srcPos = _PredictedPositions[id.x];
    float srcDensity = _Densities[id.x];
    float3 srcVelocity = _Velocities[id.x];
    int3 srcCellPos = GetCellPos(srcPos, _smoothingLength);
    
    float3 viscosityBodyForce = float3(0.0, 0.0, 0.0);
    for (int i = 0; i < NEIGHBOURHOOD_SIZE; i++) {
        int3 nbrCellPos = srcCellPos + neighbourOffsets[i];
        int nbrHash = ComputeHash(nbrCellPos);
        int nbrKey = GetKey(nbrHash, _numParticles * 2);
        HashLookup lookupData = _LookupHashIndices[nbrKey];

        if (lookupData.size == 0) { continue; } // No particles with that key
        
        for (int j = lookupData.first; j < lookupData.first + lookupData.size; j++) {
            if (nbrHash != _SortedSpatialHashedIndices[j][1]) { continue; } // Avoiding key collisions

            int nbrIdx = _SortedSpatialHashedIndices[j][0];
            float3 r = _PredictedPositions[nbrIdx] - srcPos;
            float r2 = dot(r, r);
            
            if (r2 >= _smoothingLength * _smoothingLength) { continue; } // Discard far away particles
            
            float3 nbrVelocity = _Velocities[nbrIdx];
            float nbrDensity = _Densities[nbrIdx];
            viscosityBodyForce += ((nbrVelocity - srcVelocity) / nbrDensity) * ViscosityKernel(r, _smoothingLength);
        }
    }
    viscosityBodyForce = _dynamicViscosity * _particleMass * viscosityBodyForce;
    float3 viscosityForce = viscosityBodyForce / srcDensity;
    _Velocities[id.x] += viscosityBodyForce * _deltaTime;
}

[numthreads(BLOCK_SIZE, 1, 1)]
void IntegratePosition(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }
    _Positions[id.x] += _Velocities[id.x] * _deltaTime;
}

[numthreads(BLOCK_SIZE, 1, 1)]
void HandleCollisions(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }

    for (int i = 0; i < _numColliders; i++) {
        ParticleCollider particle = { _Positions[id.x], _particleRadius };
        SimulationCollider collider = _CollidersLookup[i];

        if (collider.isBounds == 0) { // BounceDirection.OUTSIDE
            CollisionDisplacement response = GetSolidCollisionResponse(particle, collider);
            if (response.magnitude != 0.0) {
                _Positions[id.x] += response.magnitude * response.direction;
                _Velocities[id.x] = ReflectAndDampVelocity(_Velocities[id.x], response.direction, _collisionDamping);
            }
        }
        else if (collider.isBounds == 1) { // BounceDirection.INSIDE
            CollisionDisplacement response = GetHollowCollisionResponse(particle, collider);
            _Positions[id.x] += response.displacement;
            _Velocities[id.x] = ReflectAndDampVelocity(_Velocities[id.x], response.direction, _collisionDamping);
            _Velocities[id.x] += response.displacement * GetVelocityAdjustment(_Velocities[id.x], _gravity);
        }
    }  
}