// Kernels
#pragma kernel IntegratePosition
#pragma kernel ApplyExternalForces
#pragma kernel HandleCollisions
#pragma kernel CalculateDensities
#pragma kernel ComputeSpatialHashes
#pragma kernel BuildSpatialHashLookup
#pragma kernel BitonicMergeStep
#pragma kernel ComputeSortingDirections

// Helpers
#include "FluidPhysicsHelper2D.hlsl"
#include "SpatialHashHelper2D.hlsl"

// Shader properties (Common data)
float _deltaTime;
int _numParticles;
float _gravity;
float _collisionDamping;
float2 _boundsCentre;
float2 _boundsSize;
float _particleMass;
float _smoothingLength;

int _k;
int _l;

// Structured buffers (Per instance data)
RWStructuredBuffer<float2> _Positions;
RWStructuredBuffer<float2> _Velocities;
RWStructuredBuffer<float> _Densities;
RWStructuredBuffer<int2> _SortedSpatialHashedIndices;
RWStructuredBuffer<int2> _LookupHashIndices;
RWStructuredBuffer<bool> _SortingDirections;


bool IndexOutOfRange(int particleIndex, int max) {
    return particleIndex >= max;
}

float2 ReflectAndDampVelocity(float2 velocity, float2 normal) {
    return velocity - (_collisionDamping + 1) * dot(velocity, normal) * normal;
}

int Compare(int i, int j) {
    return CompareKeyFromHash(_SortedSpatialHashedIndices[i][1], _SortedSpatialHashedIndices[j][1], _numParticles * 2);
}

void Exchange(int i, int j) {
    int2 tmp = _SortedSpatialHashedIndices[i];
    _SortedSpatialHashedIndices[i] = _SortedSpatialHashedIndices[j];
    _SortedSpatialHashedIndices[j] = tmp;
}


[numthreads(64, 1, 1)]
void ComputeSpatialHashes(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }
    
    int2 cellPos = GetCellPos(_Positions[id.x], _smoothingLength);
    int hash = ComputeHash(cellPos);
    _SortedSpatialHashedIndices[id.x] = int2(id.x, hash);
    
    // Reset lookupHashIndices (size = 0, first = max int)
    _LookupHashIndices[id.x] = int2(0, 0x7FFFFFFF);
    _LookupHashIndices[id.x + _numParticles] = int2(0, 0x7FFFFFFF);
}

[numthreads(64, 1, 1)]
void ComputeSortingDirections(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles >> 1)) { return; }

    bool dir = ((id.x / (_k >> 1)) & 1) == 1;
    _SortingDirections[id.x] = dir;
}

[numthreads(64, 1, 1)]
void BitonicMergeStep(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles >> 1)) { return; }

    int i = (id.x / _l) * _l + id.x;
    int j = i + _l;
    if (_SortingDirections[id.x] != (Compare(i, j) > 0)) {
        Exchange(i, j);
    }
}

[numthreads(64, 1, 1)]
void BuildSpatialHashLookup(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }

    int hash = _SortedSpatialHashedIndices[id.x][1];
    int key = GetKey(hash, _numParticles * 2);
    int candidateFirst = id.x;

    InterlockedAdd(_LookupHashIndices[key][0], 1);
    InterlockedMin(_LookupHashIndices[key][1], candidateFirst);
}

[numthreads(64, 1, 1)]
void CalculateDensities(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }

    float2 srcPos = _Positions[id.x];
    int2 srcCellPos = GetCellPos(srcPos, _smoothingLength);
    float kernelAcc = 0.0;
    
    for (int i = 0; i < 9; i++) {
        int2 nbrCellPos = srcCellPos + neighborOffsets[i];
        int nbrHash = ComputeHash(nbrCellPos);
        int nbrKey = GetKey(nbrHash, _numParticles * 2);
        int2 lookupData = _LookupHashIndices[nbrKey];

        if (lookupData[0] == 0) { continue; } // No particles with that key

        int count = lookupData[0];
        int first = lookupData[1];
        
        for (int j = first; j < first + count; j++) {
            int particleIdx = _SortedSpatialHashedIndices[j][0];
            float2 r = _Positions[particleIdx] - srcPos;
            float r2 = dot(r, r);

            if (nbrHash != _SortedSpatialHashedIndices[j][1]) { continue; } // Avoiding key collisions
            if (r2 >= _smoothingLength * _smoothingLength) { continue; } // Discard far away particles

            float rL = sqrt(r2);
            kernelAcc += DensityKernel(rL, _smoothingLength);
        }
    }

    _Densities[id.x] = _particleMass * kernelAcc;
}

[numthreads(64, 1, 1)]
void ApplyExternalForces(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }
    _Velocities[id.x] += float2(0, _gravity) * _deltaTime;
}

[numthreads(64, 1, 1)]
void IntegratePosition(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }
    _Positions[id.x] += _Velocities[id.x] * _deltaTime;
}

[numthreads(64, 1, 1)]
void HandleCollisions(uint3 id : SV_DispatchThreadID) {
    if (IndexOutOfRange(id.x, _numParticles)) { return; }

    // Obtain the distance from the particle to the closest bounds
    float2 boundsHalfSize = 0.5 * _boundsSize;
    float2 distCentreToParticle = _Positions[id.x] - _boundsCentre;
    float2 quadrant = sign(distCentreToParticle); // The quadrant of the bounds where the particle is
    float2 dst = boundsHalfSize + quadrant * -1 * distCentreToParticle;
    
    if (dst.x <= 0) {
        _Positions[id.x].x = (quadrant * boundsHalfSize + _boundsCentre).x;
        _Velocities[id.x] = ReflectAndDampVelocity(_Velocities[id.x], float2(-1 * quadrant.x, 0));
    }

    if (dst.y <= 0) {
        _Positions[id.x].y = (quadrant * boundsHalfSize + _boundsCentre).y;
        _Velocities[id.x] = ReflectAndDampVelocity(_Velocities[id.x], float2(0, -1 * quadrant.y));
    }
}